Sub UsingADictionary():
    Dim dictData As Object
    Dim bItemExists As Boolean
    Dim tblStock As ListObject
    Dim arrData, arrReport, arrHeaders As Variant
    Dim i As Long
    Dim rng As Range
    
    'create the dictionary object
    Set dictData = CreateObject("Scripting.Dictionary")
    Set tblStock = Worksheets("A").ListObjects("Table")
    
    'put the data into an array for faster processing
    arrData = tblStock.DataBodyRange
    
    'loop through the array
    For i = 1 To UBound(arrData)
        'if key exists, add to it, else create and add to it
        If dictData.Exists(arrData(i, 1)) Then
            dictData.Item(arrData(i, 1)) = dictData.Item(arrData(i, 1)) + _
                arrData(i, 7)
        Else
            dictData.Add arrData(i, 1), arrData(i, 7)
        End If
    Next i
    
    'the location 2 columns to the right of the table
    Set rng = tblStock.Range.Offset(tblStock.Range.Rows.Count + 2).Resize(1, 1)
    
    'put the dictionary keys and values each into an array
    'then dump them on the sheet
    arrHeaders = dictData.Keys
    rng.Resize(dictData.Count, 1).Value = Application.Transpose(arrHeaders)
    arrReport = dictData.Items
    rng.Offset(, 1).Resize(dictData.Count, 1).Value = _
        Application.Transpose(arrReport)
    Set dictData = Nothing
    Set tblStock = Nothing
    Set rng = Nothing
End Sub


